buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.2.31"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.16"
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.17.0"
    id "maven"
    id "maven-publish"
    id "signing"
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:1.2.31"
    compile 'com.google.code.gson:gson:2.8.2'
    testCompile 'junit:junit:4.12'
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from dokka.outputDirectory
}

publishing {
    publications {
        MavenLocal(MavenPublication) {
            from components.java
            artifactId 'image-assert-im7'
            groupId 'com.github.annypatel.image-assert'
            version = '1.0.6'
            artifact(sourceJar) {
                classifier "sources"
            }
            artifact(javadocJar)
            pom.withXml() {
                def root = asNode()
                root.appendNode('packaging', 'jar')
                root.appendNode('name', 'Image assertion library')
                root.appendNode('description', 'Image assertion library brings image comparison into automated testing.')
                root.appendNode('url', 'https://github.com/annypatel/image-assert')
                def scm = root.appendNode('scm')
                scm.appendNode('url', 'https://github.com/annypatel/image-assert')
                scm.appendNode('connection', 'scm:https://github.com/annypatel/image-assert.git')
                scm.appendNode('developerConnection', 'scm:git://github.com/annypatel/image-assert.git')
                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The MIT License (MIT)')
                license.appendNode('url', 'http://www.opensource.org/licenses/MIT')
                license.appendNode('distribution', 'repo')
                def developer = root.appendNode('developers').appendNode('developer')
                developer.appendNode('name', 'Anny Patel')
                developer.appendNode('email', '')
            }
        }
    }
    repositories {
        maven {
            name 'snapshot'
            url getSnapshotRepositoryUrl()
            credentials {
                username = getRepositoryUsername()
                password = getRepositoryPassword()
            }
        }
        maven {
            name 'release'
            url getReleaseRepositoryUrl()
            credentials {
                username = getRepositoryUsername()
                password = getRepositoryPassword()
            }
        }
    }
}

signing {
    required { isReleaseBuild() }
    sign publishing.publications.MavenLocal
}

def isReleaseBuild() {
    def version = publishing.publications.MavenLocal.version
    return !version.contains("SNAPSHOT")
}

def getReleaseRepositoryUrl() {
    return hasProperty('RELEASE_REPOSITORY_URL') ? RELEASE_REPOSITORY_URL
            : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

def getSnapshotRepositoryUrl() {
    return hasProperty('SNAPSHOT_REPOSITORY_URL') ? SNAPSHOT_REPOSITORY_URL
            : "https://oss.sonatype.org/content/repositories/snapshots/"
}

def getRepositoryUsername() {
    return hasProperty('SONATYPE_NEXUS_USERNAME') ? SONATYPE_NEXUS_USERNAME : ""
}

def getRepositoryPassword() {
    return hasProperty('SONATYPE_NEXUS_PASSWORD') ? SONATYPE_NEXUS_PASSWORD : ""
}
